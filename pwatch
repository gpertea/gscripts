#!/usr/bin/perl
## basic process watcher utility
## list of processes to watch is maintained ~/.pwatch.lst
## a cronjob should be set up to simply call pwatch every 2 minutes or so:
## */2 * * * * /path/to/pwatch
##additional configuration is in ~/.pwatchrc
##    email=   #email to send the notification to (required)
##    msmtp=   #full path to msmtp program (required)
use strict;
my $usage=q/
 Process watch - monitoring specified running processes and notify 
 when they are gone.
 Usage:
 
  pwatch {show|pid} [<process_name>] 
     show the pids of <process_name> or of all parent processes
     of the current user, if <process_name> is not specified

  pwatch add <processID> [<label>]
     add the process with <processID> to the watchlist, so the
     user will be notified by email when the process is no longer
     active

  pwatch
     check for the running status of processs placed on the 
     watchlist with 'pwatch add'; better put this in crontab, e.g.:
       *\/2 * * * * \/path\/to\/pwatch/;

die("$usage\n") if ($ARGV[0] eq '-h' || $ARGV[0] eq '--help');
my $home=$ENV{HOME};
my $isTerm=-t STDOUT;
my $user=$ENV{USER} || $ENV{LOGNAME};
my $memdir='/dev/shm/'.$user.'-pwatch';

if (!-d $memdir) {
 mkdir($memdir) || die("Error: cannot create directory $memdir ($!)!\n");
}
my $lstfile="$memdir/watching";
my $logh;
open($logh, '>>', "$memdir/pwatch.log") || 
   die("Error opening log file $memdir/pwatch.log");
if (!$isTerm) {
 *STDERR = $logh;
}

my ($homecfg, $memcfg)=("$home/.pwatchrc", "$memdir/.pwatchrc");
my ($email, $mpath, $host);
if (! -f "$memcfg") {
   #copy the configuration file .pwatchrc to $memdir for faster subsequent access
   die("Error: file $homecfg is missing!\n")
     unless -f "$homecfg";
   system("cp $homecfg $memdir");
}
if (@ARGV==0) {
  #main use case: watching processes (usually in a cronjob)
  
  ($email, $mpath, $host)=loadConfig("$memcfg");
  #cron job checking on the processes listed in pwatch.lst
  checkProcesses();
  exit 0;
}
my $cmd=shift(@ARGV);
if ($cmd eq 'show' || $cmd eq 'pid' || $cmd eq 'list') {
  my $pname=shift(@ARGV);
  my @pids;
  if ($pname) {
    @pids=grep { !/\\_/ } 
       (split(/\n/,`ps hf -opid,state,comm,user:21,etime -C '$pname'`));
  }
  else {
   @pids=grep { !/^\s*\d+\s+[A-Z][\s\\\_\|]+[a-z]*sh\s+/ }
     (split(/\n/,`ps hf -opid,state,comm:32,user:21,etime`));
   @pids=map { 
      s/^(\s*\d+\s+[A-Z]\s+\w+)    /$1/ 
        unless s/^(\s*\d+\s+[A-Z]\s) (\\_ |[\| ]  )/$1/; 
      $_
     } @pids;
  }
  #my @pids=split(/\n/,`ps hf -opid,comm,user:21,etime -C '$pname'`);
  foreach my $pl (@pids) {
    my ($pid)=($pl=~m/^(\d+)/);
    my $wd=`readlink -e /proc/$pid/cwd`;
    chomp($wd);
    $pl.="\t$wd";
  }
  print join("\n", @pids)."\n";
  exit 0;
}
if ($cmd eq 'add') { #this is meant to be interactive (in the shell)
  my ($pid, $label)=@ARGV;
  die("Error: a numeric process ID# is required! Use 'pwatch pid processName' to find it. \n")
    if (!$pid || $pid!~/^\d+$/);
  if (!$label) {
    $label=`ps -p $pid -o comm=`;
    chomp($label);
  }
  checkConfig(); 
  #get the working directory as well:
  my $wd=`readlink -e /proc/$pid/cwd`;
  chomp($wd);
  open(LST, ">>$lstfile") || die("Error opening file $lstfile\n");
  unless ($host) { $host = qx{ hostname }; chomp($host) }
  print LST join("\t",$pid, $label, $wd, $host)."\n";
  wlog("Process $label ($pid, $wd) on $host added to watch list.");
  close(LST);
  exit 0;
}

#-----------------

sub wlog {
 print STDERR join(' ', @_)."\n";
 if ($isTerm) {
   print $logh join(' ', @_)."\n";
 }
}

sub notify {
 my ($pid, $name, $wd, $st, $h)=@_;
 my $status = $st ? "stopped ($st)" : 'ended';
 wlog("process $name ($pid) $status");
 send_email($email, "[pwatch] $name ".$status." on $h", 
  "Process '$name' (pid $pid) $status on host $h\nWorking directory: $wd");
}

sub checkProcesses {
 return unless (-s $lstfile)>1;
 my $changed;
 my @procs;
 if (!open(LST, $lstfile)) {
   wlog("Error opening file $lstfile !");
   exit(1);
 }
 while(<LST>) {
   next if m/^#/ || length($_)<2;
   my $l=$_;
   my ($pid, $name, $wd, $h)=split(/\t/,$_);
   my $st=`ps -p $pid -o state=`;
   chomp($st);
   if (!$st || $st =~ m/[ZTtXxKP]/) {
     $changed=1;
     notify($pid, $name, $wd, $st, $h);
   }
   else {
    push(@procs, $l);
   }
 }
 close(LST);
 if ($changed) {
   if (!open(LST, '>'.$lstfile)) {
     wlog("Error creating file $lstfile!\n");
     exit(1);
   }
   print LST join('', @procs);
   close(LST);
 }
}

sub loadConfig {
 open(CFG, $_[0]) || die("Error opening file $_[0]\n");
 my ($em, $cm, $hs);
 while (<CFG>) {
  next if m/^#/;
  chomp;
  if (m/^\s*(email|msmtp|host)\s*=\s*([^#]+)/i) {
    my ($s, $v)=($1,$2);
    $v=~s/\s+$//;
    if ($s eq 'email') { $em=$v }
    elsif ($s eq 'msmtp') { $cm=$v }
    elsif ($s eq 'host') { $hs=$v }
  }
 }
 close(CFG);
 return ($em, $cm, $hs);
}

sub checkConfig {
 #at 'add' time, make sure config has msmtp path and current host name
 my ($emf, $mpf, $chostf)=loadConfig($homecfg);
 my ($em, $mp, $chost)=loadConfig($memcfg);
 die("Error: target email not provided in $homecfg") unless $emf;
 if ($em ne $emf || !$mp || !$chost) {
     my @h=split(/\./, $ENV{HOSTNAME});
     $chost=$h[0];
     $mp=`which msmtp`;
     chomp($mp);
     die("Error: msmtp program not found!\n") unless $mp;
     open(CFG, ">$memcfg") || die("Error creating $memcfg !\n");
     print CFG "email=$emf\nmsmtp=$mp\nhost=$chost\n";
     close(CFG);
 }
}

sub send_email {
 my ($to, $subj, $body)=@_;
 open(MSMTP, "| $mpath --tls-certcheck=off -t '$to'") 
   || die("Error opening the msmtp pipe!\n");
 print MSMTP "To: <$to>\nSubject: $subj\n\n$body\n";
 close(MSMTP);
}
